/*
 * Copyright 2015 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

package kie.live;

global java.util.Deque controlSet;
global java.util.Deque usersNotified;

rule "heartbeat rule"
when
	$h : HeartBeat( $ts: ts)
	not(HeartBeat( this != $h, this after[0s, 5s] $h))
then
    System.out.println("Heartbeat not received in 5s after: " +  $ts);
	controlSet.push($h); //  clear this in production
end

rule "Computer is rebooting too many times"
when
    $r1 : SystemRebootEvent($cId : computerId, $uId: userId)
    $numberOfTimes : Number(this >= 2) from accumulate(
        $r2 : SystemRebootEvent(this != $r1,
                                computerId == $cId,
                                this meets[1h] $r1),
                                count($r2)
                                )
    $u : User(id == $uId, notified == false)
then
    System.out.println($u.getUsername() + ", your computer was rebooted " + $numberOfTimes + " times after a reboot" );
    modify($u) {
        setNotified(true);
    }
	usersNotified.push($u); // clear this in production
end
